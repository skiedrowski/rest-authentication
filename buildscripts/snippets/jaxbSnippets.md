This build script is pretty similar to [jaxwsSnippets](jaxwsSnippets.md).


    configurations { jaxb }

    dependencies {
        jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7'
        jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7'
        //jaxb plugin used for toString
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics:$ver.jaxb2_basics" 
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-ant:$ver.jaxb2_basics"
    }

    //ext.genSourcesDir = "src/jaxb/java"
    //clean { delete genSourcesDir }

    sourceSets {
            jaxbHelper {
                //contains all sources which are referenced by jaxb-XSDs/XJBs
                compileClasspath = main.compileClasspath
            }

        jaxb {
            //sources generated by xjc
            //sources compiled from a build-subdir do not end up in the IntelliJ artifact when payara is started from IntelliJ
            //this only works when using gradle to build the war
            java.srcDir "${buildDir}/generated-sources/jaxb/"
            //using src/jaxb (or the default) allows deploying the IntelliJ generated artifact
	    //if this is used, you should also delete this dir on clean (see above)
            //java.srcDir "$genSourcesDir"
            compileClasspath = main.compileClasspath + jaxbHelper.output
            //runtimeClasspath += main.output
	    }

        main {
            compileClasspath += jaxb.output
            runtimeClasspath += jaxb.output //+ jaxb.runtimeClasspath
        }

        test {
            compileClasspath += jaxb.output + jaxbHelper.output
            runtimeClasspath += jaxb.output + jaxbHelper.output
        }
    }

    jar {
        from sourceSets.jaxb.output
        from sourceSets.jaxbHelper.output
    }

    task jaxb() {
        doLast {
            System.setProperty('javax.xml.accessExternalSchema', 'all')
            //original
            //ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)
            //when using jaxb2-basics plugin(s)
            ant.taskdef(name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
            ant.jaxbTargetDir = sourceSets.jaxb.java.srcDirs.last()
            ant.jaxbTargetDir.mkdirs()
            ant.xjc(
                destdir: "${ant.jaxbTargetDir}",
                /*encoding: 'UTF-8,*/
                extension: 'true',
                target: '2.1') {
                    schema(dir: 'src/main/resources', includes: "**/*.xsd")
                    binding(dir: 'src/main/resources', includes: "**/*.xjb")
                    arg(value: '-npa')
                    arg(value: '-XtoString') //requires jabx2-basics
            }
        }
    }
    
    compileJava.dependsOn jaxb
	